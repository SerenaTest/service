plugins {
	id 'org.springframework.boot' version '3.4.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '2.4.0'
	id 'java'
	id 'jacoco'
}

group = 'ro.unibuc'
version = '0.0.1-SNAPSHOT'
java {
	sourceCompatibility = JavaVersion.VERSION_21
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	def cucumberVersion = "7.20.1"
	def junitVersion = "5.11.4"
	def testcontainersVersion = "2.0.3"

	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.session:spring-session-core'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.data:spring-data-mongodb'
	implementation 'commons-io:commons-io:2.18.0'
	implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'

	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.11.4'

	// Integration tests - test containers dependencies
    testImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
    testImplementation "org.testcontainers:testcontainers-mongodb:$testcontainersVersion"

	//	E2E tests
	runtimeOnly "org.junit.vintage:junit-vintage-engine:$junitVersion"
	testImplementation 'org.apache.httpcomponents.client5:httpclient5:5.4.1'
	testImplementation "io.cucumber:cucumber-core:$cucumberVersion"
	testImplementation "io.cucumber:cucumber-java:$cucumberVersion"
	testImplementation "io.cucumber:cucumber-junit:$cucumberVersion"
	testImplementation "io.cucumber:cucumber-spring:$cucumberVersion"
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform {
		excludeTags ("IntegrationTest", "E2E")
	}
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.9"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
				'**/config/**',
				'**/exception/**',
				'**/model/**',
				'**/repository/**',
				'**/request/**',
				'**/response/**',
				'**/*Application.class'
			])
		}))
	}
}

task testIT(type: Test) {
	outputs.dir snippetsDir
	outputs.upToDateWhen { false }

	useJUnitPlatform {
		includeTags "IntegrationTest"
	}
}

configurations {
	cucumberRuntime {
		extendsFrom testImplementation
	}
}

task testE2E() {
	dependsOn assemble, testClasses
	doLast {
		javaexec {
			mainClass.set("io.cucumber.core.cli.Main")
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = [
					'--plugin', 'pretty',
					'--plugin', 'html:build/reports/cucumber/cucumber-report.html',
					'--glue', 'ro.unibuc.prodeng.e2e',
					'src/test/resources']
		}
	}
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}
